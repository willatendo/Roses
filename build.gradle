plugins {
    id 'eclipse'
    id 'maven-publish'
    id 'net.minecraftforge.gradle' version '5.1.+'
	id 'com.github.johnrengelman.shadow' version '7.1.0'
}

apply from: 'https://raw.githubusercontent.com/SizableShrimp/Forge-Class-Remapper/main/classremapper.gradle'

configurations {
	shade
}

version = '4.1.0'
group = 'roses'
archivesBaseName = 'roses'

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

minecraft {
	mappings channel: 'official', version: minecraft_version
	
	accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')
	
	runs {
		client {
			workingDirectory project.file('run')

			mods {
				roses {
					source sourceSets.main
				}
			}
		}
			
		data {
			workingDirectory project.file('run')
			
			args '--mod', 'roses', '--all', '--existing', file('src/main/resources').toString(), '--existing', file('src/generated/resources').toString(), '--output', file('src/generated/resources/')
					
			mods {
				roses {
					sourceSets.main
				}
			}
		}
	}
}

sourceSets.main.resources {
    srcDir 'src/generated/resources'
}
	
repositories {
	maven {
		url "https://www.cursemaven.com"
		content {
			includeGroup "curse.maven"
		}
	}
	maven { // Registrate
		url "https://maven.tterrag.com/"
	}
}

dependencies {
	minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"
	
	def registrate = "com.tterrag.registrate:Registrate:MC${major_version}-${registrate_version}"
	implementation fg.deobf(registrate)
	shade registrate
}

jar {
	manifest {
		attributes([
			"Specification-Title": "roses",
			"Specification-Vendor": "Willatendo",
			"Specification-Version": "4.1.0",
			"Implementation-Title": project.name,
			"Implementation-Version": "${version}",
			"Implementation-Vendor" :"Willatendo",
			"Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
		])
	}
}

shadowJar {
	classifier = ''
	configurations = [project.configurations.shade]
	relocate 'com.tterrag.registrate', 'roses.repack.registrate'
}

reobf {
	shadowJar {}
}

build.dependsOn shadowJar
build.dependsOn reobfShadowJar

jar.finalizedBy('reobfJar')